#include <Servo.h>

// Arduino pin assignment
#define PIN_IR    0         // IR sensor at Pin A0
#define PIN_LED   9
#define PIN_SERVO 10

// --- Servo duty (Î¼s) ---
#define _DUTY_MIN 1100     
#define _DUTY_MAX 4000      
#define _DUTY_NEU ((_DUTY_MIN + _DUTY_MAX) / 2)  

// --- Distance range (mm) ---
#define _DIST_MIN  100.0    
#define _DIST_MAX  250.0   

// --- Filters/loop ---
#define EMA_ALPHA  0.30  
#define LOOP_INTERVAL 20  

Servo myservo;
unsigned long last_loop_time = 0; 

float dist_prev = _DIST_MIN;
float dist_ema  = _DIST_MIN;

void setup()
{
  pinMode(PIN_LED, OUTPUT);

  myservo.attach(PIN_SERVO);
  myservo.writeMicroseconds(_DUTY_NEU);

  Serial.begin(1000000);
  last_loop_time = millis();
}

void loop()
{
  unsigned long time_curr = millis();
  int duty;
  float a_value, dist_raw;


  if (time_curr < (last_loop_time + LOOP_INTERVAL))
    return;
  last_loop_time += LOOP_INTERVAL;

  a_value = analogRead(PIN_IR);

 
  if (a_value <= 10.0) a_value = 10.0;


  dist_raw = ((6762.0 / (a_value - 9.0)) - 4.0) * 10.0;


  float dist_filt = dist_raw;
  if (dist_filt < _DIST_MIN) dist_filt = _DIST_MIN;
  if (dist_filt > _DIST_MAX) dist_filt = _DIST_MAX;

  if (dist_raw >= _DIST_MIN && dist_raw <= _DIST_MAX) {
    digitalWrite(PIN_LED, HIGH);
  } else {
    digitalWrite(PIN_LED, LOW);
  }

 
  dist_ema = EMA_ALPHA * dist_filt + (1.0 - EMA_ALPHA) * dist_ema;

  
  float duty_f =
      _DUTY_MIN +
      (dist_ema - _DIST_MIN) * (float)(_DUTY_MAX - _DUTY_MIN) /
      (_DIST_MAX - _DIST_MIN);


  if (duty_f < _DUTY_MIN) duty_f = _DUTY_MIN;
  if (duty_f > _DUTY_MAX) duty_f = _DUTY_MAX;

  duty = (int)(duty_f + 0.5f); 
  myservo.writeMicroseconds(duty);


  Serial.print("_DUTY_MIN:");  Serial.print(_DUTY_MIN);
  Serial.print("_DIST_MIN:");  Serial.print(_DIST_MIN);
  Serial.print(",IR:");        Serial.print(a_value);
  Serial.print(",dist_raw:");  Serial.print(dist_raw);
  Serial.print(",ema:");       Serial.print(dist_ema);
  Serial.print(",servo:");     Serial.print(duty);
  Serial.print(",_DIST_MAX:"); Serial.print(_DIST_MAX);
  Serial.print(",_DUTY_MAX:"); Serial.print(_DUTY_MAX);
  Serial.println("");
}
